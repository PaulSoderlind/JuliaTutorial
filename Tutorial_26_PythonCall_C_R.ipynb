{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Running Python, C or R Code from Julia\n",
    "\n",
    "This notebook first provides a basic introduction to how to run Python, using the `PythonCall.jl` package and also C code (requires no package) from Julia. Please see the [PythonCall.jl](https://github.com/JuliaPy/PythonCall.jl) homepage for instructions for how to either use an existing Python installation, or let the package make one.\n",
    " \n",
    "An alternative package (not used here) for running Python is [PyCall.jl](https://github.com/JuliaPy/PyCall.jl), which is a slightly older approach.\n",
    "\n",
    "The second part of the notebook runs R code, using the [RCall.jl](https://github.com/JuliaInterop/RCall.jl) package, from Julia. See the the RCall.jl homepage (the installation instructions) for how to use your existing R installation or for letting RCall.jl install R for you."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {
    "tags": []
   },
   "outputs": [],
   "source": [
    "using Printf, DelimitedFiles\n",
    "include(\"src/printmat.jl\");"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Load Data"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {
    "tags": []
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "OLS coeffs according to Julia\n",
      "    -0.504\n",
      "     1.341\n",
      "\n"
     ]
    }
   ],
   "source": [
    "x = readdlm(\"Data/MyData.csv\",',',skipstart=1)  #reading the csv file\n",
    "\n",
    "(Rme,Rf,R) = (x[:,2],x[:,3],x[:,4])  #creating variables from columns of x\n",
    "y  = R - Rf                          #do R .- Rf if R has several columns\n",
    "\n",
    "c = ones(length(Rme))\n",
    "x = [c Rme]\n",
    "\n",
    "b = x\\y\n",
    "println(\"OLS coeffs according to Julia\")\n",
    "printmat(b)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Python\n",
    "\n",
    "You need Python's `statsmodels` package installed to run the code below. If you have let PythonCall install Python for you, use [CondaPkg.jl](https://github.com/JuliaPy/CondaPkg.jl) to add packages: `import CondaPkg; CondaPkg.add(\"statsmodels\")`.\n",
    "\n",
    "In the next cells we *(a)* load the PythonCall.jl package and activates the (Python) package `statsmodels`; *(b)* call some functions (eg. `OLS()`) from statsmodels.\n",
    "\n",
    "In many cases, you can use Julia arrays (like `y` and `x` in the cells below) in a call on a Python function. In other cases (like here), you need to convert them to numpy arrays."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {
    "tags": []
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n"
     ]
    }
   ],
   "source": [
    "using PythonCall\n",
    "sm = pyimport(\"statsmodels.api\")     #activate this package and call it `sm`\n",
    "np = pyimport(\"numpy\")\n",
    "println(\"\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {
    "tags": []
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "                            OLS Regression Results                            \n",
      "==============================================================================\n",
      "Dep. Variable:                      y   R-squared:                       0.519\n",
      "Model:                            OLS   Adj. R-squared:                  0.518\n",
      "Method:                 Least Squares   F-statistic:                     416.2\n",
      "Date:                Mon, 17 Nov 2025   Prob (F-statistic):           2.72e-63\n",
      "Time:                        11:44:09   Log-Likelihood:                -1241.7\n",
      "No. Observations:                 388   AIC:                             2487.\n",
      "Df Residuals:                     386   BIC:                             2495.\n",
      "Df Model:                           1                                         \n",
      "Covariance Type:            nonrobust                                         \n",
      "==============================================================================\n",
      "                 coef    std err          t      P>|t|      [0.025      0.975]\n",
      "------------------------------------------------------------------------------\n",
      "const         -0.5042      0.305     -1.654      0.099      -1.103       0.095\n",
      "x1             1.3410      0.066     20.401      0.000       1.212       1.470\n",
      "==============================================================================\n",
      "Omnibus:                      259.682   Durbin-Watson:                   1.870\n",
      "Prob(Omnibus):                  0.000   Jarque-Bera (JB):             5249.944\n",
      "Skew:                           2.482   Prob(JB):                         0.00\n",
      "Kurtosis:                      20.323   Cond. No.                         4.68\n",
      "==============================================================================\n",
      "\n",
      "Notes:\n",
      "[1] Standard Errors assume that the covariance matrix of the errors is correctly specified.\n"
     ]
    }
   ],
   "source": [
    "resultsP = sm.OLS(np.array(y), np.array(x)).fit()   #np.array() to convert to Python\n",
    "println(resultsP.summary())"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {
    "tags": []
   },
   "outputs": [],
   "source": [
    "#foreach(println,pydir(resultsP))          #run this to see all attributes"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {
    "tags": []
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\u001b[34m\u001b[1mComparing the estimates in Julia and Python:\u001b[22m\u001b[39m\n",
      "          Julia     Julia view         Py->Ju\n",
      "         -0.504         -0.504         -0.504\n",
      "          1.341          1.341          1.341\n",
      "\n"
     ]
    }
   ],
   "source": [
    "b_P1 = PyArray(resultsP.params)                   #a Julia view of the Python array,\n",
    "b_P2 = pyconvert(Array{Float64}, resultsP.params)   #or copy/convert to a Julia array\n",
    "\n",
    "printblue(\"Comparing the estimates in Julia and Python:\")\n",
    "printmat(b,b_P1,b_P2;colNames=[\"Julia\",\"Julia view\",\"Py->Ju\"],width=15)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "We can also run Python code (quoted in \"\"\" \"\"\") directly as in the next cell."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "    -0.504\n",
      "     1.341\n",
      "\n"
     ]
    }
   ],
   "source": [
    "@pyexec (x=x, y=y) => \"\"\"\n",
    "import numpy as np\n",
    "x = np.array(x)\n",
    "y = np.array(y)\n",
    "xx = np.matmul(x.T, x)\n",
    "xy = np.matmul(x.T, y)\n",
    "b_p = np.linalg.solve(xx, xy)\n",
    "\"\"\" => b_p\n",
    "\n",
    "b_P3 = PyArray(b_p)\n",
    "printmat(b_P3)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# C\n",
    "\n",
    "This section shows some simple examples of how to call a C function. The functions are in the file `My_C_Stuff.c` (printed in the next cell). The first function `c_dot` defines a dot product between two vectors and the second function `c_ols` a simple linear regression."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "metadata": {
    "tags": []
   },
   "outputs": [],
   "source": [
    "#println(read(\"Data/My_C_Stuff.c\",String))   #uncomment to see the c file"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "To compile to a dynamlic library (dll on windows), I use gcc (for x86_64) from [mingw-64](http://mingw-w64.org)\n",
    "and run the following in the mingw terminal\n",
    "```\n",
    "gcc -shared -fPIC My_C_Stuff.c -o My_C_Stuff.dll\n",
    "```\n",
    "\n",
    "To call the C functions, place the dll file in the current folder and then run the following cells."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## A Function which Returns a Number\n",
    "\n",
    "In the next example, we use the function `c_dot` in `My_C_Stuff.dll`. The function calculates the inner product of two vectors.\n",
    "\n",
    "The details are:\n",
    "1. `mylibc.c_dot` is the library.function\n",
    "2. `length(y)::Csize_t` is the first input and its type (an integer indicating the number of elements in `y`)\n",
    "3. `y::Ptr{Float64}` is the second input (a pointer to an array of Floats) and similarly for `x2`\n",
    "4. `Float64` is the type of the output\n",
    "\n",
    "(We could potentially wrap this in a Julia function that checks for the right input types and outputs the result.)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 9,
   "metadata": {
    "tags": []
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "The inner product of x2 and y in Julia and C:   11071.648           11071.648\n"
     ]
    }
   ],
   "source": [
    "mylibc = \"My_C_Stuff.dll\"\n",
    "\n",
    "x2     = x[:,2];               #get a vector with the regressor values\n",
    "\n",
    "z = @ccall mylibc.c_dot(length(y)::Csize_t, y::Ptr{Float64}, x2::Ptr{Float64})::Float64\n",
    "\n",
    "printlnPs(\"The inner product of x2 and y in Julia and C:  \",x2'y,\" \",z)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## A Function which Returns a Vector\n",
    "\n",
    "The details are as above, except that \n",
    "1. `mylibc.c_ols` is the library.function\n",
    "2. `Cvoid` is the type of the output, which here indicates that the function does not have an output. Rather, the function modifies the vector `b_c` by putting the OLS results there."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "metadata": {
    "tags": []
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Comparing the estimates in Julia and C\n",
      "     Julia         C\n",
      "    -0.504    -0.504\n",
      "     1.341     1.341\n",
      "\n"
     ]
    }
   ],
   "source": [
    "b_c = zeros(2)          #where C will store the regression results\n",
    "\n",
    "@ccall mylibc.c_ols(length(y)::Csize_t, y::Ptr{Float64}, x2::Ptr{Float64}, b_c::Ptr{Float64})::Cvoid\n",
    "\n",
    "println(\"Comparing the estimates in Julia and C\")\n",
    "printmat([b b_c];colNames=[\"Julia\",\"C\"])"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# R\n",
    "\n",
    "To avoid naming conflicts with the Python packges, *restart the kernel* and work from here.\n",
    "\n",
    "The first cell loads the data into Julia (again). The subsequent cells load the RCall.jl package, transfers the data from Julia to R (`@rput`) and then run an OLS regression (`reval()`)."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "OLS coeffs according to Julia\n",
      "    -0.504\n",
      "     1.341\n",
      "\n"
     ]
    }
   ],
   "source": [
    "using Printf, DelimitedFiles\n",
    "include(\"src/printmat.jl\")          #quickly redoing the Julia part\n",
    "\n",
    "x = readdlm(\"Data/MyData.csv\",',',skipstart=1)  #reading the csv file\n",
    "(Rme,Rf,R) = (x[:,2],x[:,3],x[:,4])  #creating variables from columns of x\n",
    "y  = R - Rf                          #do R .- Rf if R has several columns\n",
    "c = ones(length(Rme))\n",
    "x = [c Rme]\n",
    "b = x\\y\n",
    "println(\"OLS coeffs according to Julia\")\n",
    "printmat(b)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [],
   "source": [
    "using RCall"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "RObject{VecSxp}\n",
      "\n",
      "Call:\n",
      "lm(formula = y ~ x - 1)\n",
      "\n",
      "Residuals:\n",
      "    Min      1Q  Median      3Q     Max \n",
      "-17.981  -3.131  -0.359   2.281  52.361 \n",
      "\n",
      "Coefficients:\n",
      "   Estimate Std. Error t value Pr(>|t|)    \n",
      "x1 -0.50416    0.30483  -1.654    0.099 .  \n",
      "x2  1.34105    0.06573  20.401   <2e-16 ***\n",
      "---\n",
      "Signif. codes:  0 '***' 0.001 '**' 0.01 '*' 0.05 '.' 0.1 ' ' 1\n",
      "\n",
      "Residual standard error: 5.954 on 386 degrees of freedom\n",
      "Multiple R-squared:  0.5194,\tAdjusted R-squared:  0.5169 \n",
      "F-statistic: 208.6 on 2 and 386 DF,  p-value: < 2.2e-16\n",
      "\n",
      "\n"
     ]
    }
   ],
   "source": [
    "@rput x y                          #send x and y to R\n",
    "\n",
    "resultsR = reval(\"summary(mod <- lm(y ~ x-1))\")  #run R code\n",
    "println(resultsR)                                #and print output"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "[:call, :terms, :residuals, :coefficients, :aliased, :sigma, :df, Symbol(\"r.squared\"), Symbol(\"adj.r.squared\"), :fstatistic, Symbol(\"cov.unscaled\")]\n"
     ]
    }
   ],
   "source": [
    "println(names(resultsR))           #print all keys (field names)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\u001b[34m\u001b[1mComparing the estimates in Julia and R\u001b[22m\u001b[39m\n",
      "     Julia         R\n",
      "    -0.504    -0.504\n",
      "     1.341     1.341\n",
      "\n"
     ]
    }
   ],
   "source": [
    "b_R = rcopy(resultsR[:coefficients])  #the numerical results are now a Julia array\n",
    "\n",
    "printblue(\"Comparing the estimates in Julia and R\")\n",
    "printmat([b b_R[:,1]];colNames=[\"Julia\",\"R\"])"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "     Julia         R  R ver, 2\n",
      "    -0.504    -0.504    -0.504\n",
      "     1.341     1.341     1.341\n",
      "\n"
     ]
    }
   ],
   "source": [
    "R\"model <- lm(y ~ x-1)\"                              #an alternative approach\n",
    "resultsR2 = rcall(:summary, R\"model\")\n",
    "b_R2      = rcopy(resultsR2[:coefficients])\n",
    "printmat([b b_R[:,1] b_R2[:,1]];colNames=[\"Julia\",\"R\",\"R ver, 2\"])"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "We can also run blocks of R code `R\"\"\" code \"\"\"` like in the cell below. \n",
    "Do `@rput x y` or use `$x` and `$y` in the code below."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "RObject{RealSxp}\n",
       "           [,1]\n",
       "[1,] -0.5041626\n",
       "[2,]  1.3410486\n"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "#solve x'x*b = x'y\n",
    "R\"\"\"\n",
    "xx <- t(x)%*%x\n",
    "xy <- t(x)%*%y\n",
    "b_R <- solve(xx,xy)\n",
    "\"\"\""
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": []
  }
 ],
 "metadata": {
  "@webio": {
   "lastCommId": null,
   "lastKernelId": null
  },
  "kernelspec": {
   "display_name": "Julia 1.12.1",
   "language": "julia",
   "name": "julia-1.12"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
